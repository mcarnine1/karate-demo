buildscript {
    ext {
        springBootVersion = '2.2.0.RELEASE'
        gradleVersionProperty = '5.6.3'
        karateVersion = '0.9.4'
        masterThoughtVersion = '3.8.0'
        activeMqVersion = '5.15.2'
    }
    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
        maven { url "http://repo.spring.io/release" }
        maven { url "http://repo.spring.io/milestone" }
        maven { url "http://repo.spring.io/snapshot" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

jar {
    baseName = 'karate-demo'
    version = karateVersion
}

bootRun {
    systemProperties = System.properties
}

// In this section you declare where to find the dependencies of your project
repositories {
    jcenter()
    mavenLocal()
}

sourceSets {
    test {
        java {
            // Excluding UIRunner files as these require the javafx libraries
            // which are not shipped with OpenJDK. These UIRunner classes are
            // classes that allow developers to run/debug karate tests via a UI
            // and as such are not required for headless runs on jenkins server
            // but can run happily via IDE of the developer without needed to be
            // compiled by gradle.
            srcDir file('src/test/java')
            exclude '**/*UiRunner*.java'
        }
        resources {
            // Using recommended karate project layout where karate feature files
            // and associated javascript resources sit in same /test/java folders
            // as their java counterparts.
            srcDir file('src/test/java')
            exclude '**/*.java'
        }
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework:spring-jdbc'
    implementation 'org.springframework:spring-websocket'
    implementation 'com.github.java-json-tools:json-schema-validator:2.2.8'
    implementation 'commons-io:commons-io:2.5'
    implementation("javax.xml.bind:jaxb-api:2.3.1")
    runtime 'com.h2database:h2:1.4.196'
    testImplementation 'junit:junit'
    testImplementation 'com.intuit.karate:karate-ui:' + karateVersion
    testImplementation 'com.intuit.karate:karate-junit4:' + karateVersion
    testImplementation 'com.intuit.karate:karate-apache:' + karateVersion
    testImplementation 'org.apache.activemq:activemq-broker:' + activeMqVersion
    testImplementation 'org.apache.activemq:activemq-client:' + activeMqVersion
    testImplementation 'org.apache.activemq:activemq-kahadb-store:' + activeMqVersion
    testImplementation 'org.apache.geronimo.specs:geronimo-jms_1.1_spec:1.1.1'
    testImplementation 'net.masterthought:cucumber-reporting:' + masterThoughtVersion
    testRuntime("com.sun.xml.bind:jaxb-core:2.3.0")
    testRuntime("com.sun.xml.bind:jaxb-impl:2.3.2")
}

test {
    // When running the test task, only run the base Test class which runs all cucumber
    // tests in parallel.
    include '**/*DemoTestParallel*'
    // Pull karate options into the runtime
    systemProperty "karate.options", System.properties.getProperty("karate.options")
    // Pull karate options into the JVM
    systemProperty "karate.env", System.properties.getProperty("karate.env")
    // Ensure tests are always run
    outputs.upToDateWhen { false }
    // attach debugger
    if (System.getProperty('debug', 'false') == 'true') {
        jvmArgs '-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=9009'
    }
//    testLogging.showStandardStreams = true
}

wrapper {
    gradleVersion = gradleVersionProperty
}
